apply plugin: 'ch.netzwerg.release'

buildscript {
    ext {
        targetApi = 28
        sdkVersion = 'android-P'
        buildTools = '27.0.3'
        realmVersion = '5.0.0'
        appCompatVersion = '1.0.0-alpha1'
        recyclerViewVersion = '1.0.0-alpha1'
    }

    repositories {
        google()
        jcenter()
        maven {
            url 'http://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha14'                    // Android Build tools
        classpath 'ch.netzwerg:gradle-release-plugin:1.2.0'                 // Manage Release tags / versioning
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'    // Allows you to install AAR's locally
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4'   // OJO integration
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'      // Bintray integration
        classpath "io.realm:realm-gradle-plugin:${realmVersion}"            // Realm base library
    }
}

// Don't cache SNAPSHOT (changing) dependencies.
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

allprojects {
    group = 'io.realm'
    version = file("${rootDir}/version.txt").text.trim();
    repositories {
        google()
        jcenter()
        maven {
            url 'http://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
    }
}

// Configure ch.netzwerg.release plugin
// See https://github.com/netzwerg/gradle-release-plugin
task build {}
release {
    push = false
    versionSuffix = '-SNAPSHOT'
    tagPrefix = 'v'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

